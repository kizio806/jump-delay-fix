plugins {
    id 'java-library'
    id 'jacoco'
}

base {
    archivesName = "${mod_id}-common-${minecraft_version}"
}

repositories {
    mavenCentral()
}

dependencies {
    api 'org.slf4j:slf4j-api:2.0.17'

    testImplementation platform('org.junit:junit-bom:5.11.4')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

jacoco {
    toolVersion = '0.8.12'
}

tasks.named('test') {
    finalizedBy(tasks.named('jacocoTestReport'))
}

tasks.named('jacocoTestReport') {
    dependsOn(tasks.named('test'))

    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

tasks.named('jacocoTestCoverageVerification') {
    dependsOn(tasks.named('jacocoTestReport'))

    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.75
            }
        }
    }
}

tasks.named('check') {
    dependsOn(tasks.named('jacocoTestCoverageVerification'))
}
